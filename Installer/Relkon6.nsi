; Script generated by the HM NIS Edit Script Wizard.
RequestExecutionLevel user

!include x64.nsh
!include WordFunc.nsh
!insertmacro VersionCompare
!include LogicLib.nsh

SetCompressor /SOLID lzma

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Relkon"
!define PRODUCT_VERSION "6.4" ; To be replaced by Build.exe
!define PRODUCT_PUBLISHER "ООО ""КОНТЭЛ"""
!define PRODUCT_WEB_SITE "http://kontel.ru"
!define PRODUCT_UNINST_KEY "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"


!define MULTIUSER_EXECUTIONLEVEL User
!define MULTIUSER_INSTALLMODE_DEFAULT_CURRENTUSER
!define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY "SHELL_CONTEXT"
!define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_VALUENAME "InstallDir"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_KEY "SHELL_CONTEXT"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUENAME "InstallDir"
!define MULTIUSER_INSTALLMODE_INSTDIR "${PRODUCT_NAME} ${PRODUCT_VERSION}"
;!include MultiUser.nsh ; Doesn't really do what we need to (goes to AppData instead of Documents, no x64 support)

; MUI 1.67 compatible ------
!include "MUI.nsh"


; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
;!insertmacro MUI_PAGE_WELCOME
; License page;
; Directory page
!insertmacro MUI_PAGE_DIRECTORY

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "Russian"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI END ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "..\SetupRelkon.exe"
InstallDir "${MULTIUSER_INSTALLMODE_INSTDIR}"
ShowInstDetails show
ShowUnInstDetails show

Function .onInit
  StrCpy $INSTDIR "$DOCUMENTS\${MULTIUSER_INSTALLMODE_INSTDIR}"
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

; The "" makes the section hidden.
Section "" SecUninstallPrevious

    Call UninstallPrevious
		
		SetOutPath $INSTDIR
		File /x *.pdb /x *.manifest /x *.application /x *.vshost.exe /x *.config ..\Distr\*.*
		SetOutPath $INSTDIR\arm-gcc
		File /r ..\Distr\arm-gcc\*.*
		SetOutPath $INSTDIR\firmware
		File /r /x fc_u.o /x *.elf /x *.bat /x *.err ..\Distr\firmware\*.*		
		
		CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME} ${PRODUCT_VERSION}"
		CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME} ${PRODUCT_VERSION}\Удалить ${PRODUCT_NAME} ${PRODUCT_VERSION}.lnk" "$INSTDIR\uninst.exe" "" "$INSTDIR\uninst.exe" 0
		CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME} ${PRODUCT_VERSION}\Запустить ${PRODUCT_NAME} ${PRODUCT_VERSION}.lnk" "$INSTDIR\Relkon6.exe" "" "$INSTDIR\Relkon6.exe" 0
		CreateShortCut "$DESKTOP\${PRODUCT_NAME} ${PRODUCT_VERSION}.lnk" "$INSTDIR\Relkon6.exe" "" "$INSTDIR\Relkon6.exe" 0
						
		${If} ${RunningX64}
			SetRegView 64
		${EndIf}
		
		WriteUninstaller "$INSTDIR\uninst.exe"
		WriteRegStr ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
		WriteRegStr ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY} "${PRODUCT_UNINST_KEY}" "InstallDir" "$INSTDIR"
		WriteRegStr ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
		WriteRegStr ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
		WriteRegStr ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
		
		WriteRegStr HKCR ".rp6" "" "Kontel Relkon 6 Project File"
		WriteRegStr HKCR ".rp6\DefaultIcon" "" "$INSTDIR\Relkon6.exe,1"
		
		
		WriteRegStr HKCR ".rp6\shell" "" "open"
		WriteRegStr HKCR ".rp6\shell\open\command" "" '"$INSTDIR\Relkon6.exe" "%1"' 
  
		
		WriteRegStr HKCR ".rp6" "" "Relkon6.Project"
		WriteRegStr HKCR "Relkon6.Project" "" "Relkon6 Project File"
		WriteRegStr HKCR "Relkon6.Project\DefaultIcon" "" "$INSTDIR\Relkon6.exe,1"		
    WriteRegStr HKCR "Relkon6.Project\shell" "&Open" "open"
    WriteRegStr HKCR "Relkon6.Project\shell\open\command" "" '$INSTDIR\Relkon6.exe "%1"'
		
		DeleteRegValue HKCU "Software\Microsoft\Windows\CurrentVersion\Explorer\FileExts\.rp6" "Application"
		
SectionEnd

Function UninstallPrevious

    ; Check for uninstaller.
    ReadRegStr $R0 ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString"
    ReadRegStr $R1 ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY} "${PRODUCT_UNINST_KEY}" "InstallDir"

    ${If} $R0 == ""
        Goto Done
    ${EndIf}

    DetailPrint "Removing previous installation."

    ; Run the uninstaller silently.
    ; The "if" is necessary becaused older installer versions
    ; did not set "InstallDir".
    CopyFiles "$R0" "$TEMP\uninst.exe"
    ${If} $R1 != ""
      ExecWait '$TEMP\uninst.exe /S _?=$R1'
    ${Else}
      Push $R0
      Call GetParent
      Pop $R0
      ExecWait '$TEMP\uninst.exe /S _?=$R0'
    ${EndIf}

    Done:
    
FunctionEnd

 ; GetParent
 ; input, top of stack  (e.g. C:\Program Files\Poop)
 ; output, top of stack (replaces, with e.g. C:\Program Files)
 ; modifies no other variables.
 ;
 ; Usage:
 ;   Push "C:\Program Files\Directory\Whatever"
 ;   Call GetParent
 ;   Pop $R0
 ;   ; at this point $R0 will equal "C:\Program Files\Directory"
 Function GetParent
 
  Exch $R0
  Push $R1
  Push $R2
  Push $R3
 
  StrCpy $R1 0
  StrLen $R2 $R0
 
  loop:
    IntOp $R1 $R1 + 1
    IntCmp $R1 $R2 get 0 get
    StrCpy $R3 $R0 1 -$R1
    StrCmp $R3 "\" get
  Goto loop

  get:
    StrCpy $R0 $R0 -$R1
 
    Pop $R3
    Pop $R2
    Pop $R1
    Exch $R0
 
FunctionEnd


Function un.onInit
  !insertmacro MUI_UNGETLANGUAGE
  IfSilent +3
    MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Вы действительно желаете полностью удалить ${PRODUCT_NAME} ${PRODUCT_VERSION}?" IDYES +2
    Abort
FunctionEnd

Section Uninstall
  ${If} ${RunningX64}
    SetRegView 64
  ${EndIf}

  Delete "$INSTDIR\uninst.exe"

  RMDir /r "$SMPROGRAMS\${PRODUCT_NAME} ${PRODUCT_VERSION}" 
	Delete "$DESKTOP\${PRODUCT_NAME} ${PRODUCT_VERSION}.lnk"
	
	RMDir /r "$INSTDIR"
	
;  Delete "$INSTDIR\C.xml"
;	  Delete "$INSTDIR\Kontel.Components.dll"
;		  Delete "$INSTDIR\Kontel.IO.dll"
;			  Delete "$INSTDIR\Map.xml"
;				  Delete "$INSTDIR\QWhale.Common.dll"
;					  Delete "$INSTDIR\QWhale.Editor.dll"
;						  Delete "$INSTDIR\QWhale.Syntax.dll"
;							  Delete "$INSTDIR\QWhale.Syntax.Parsers.dll"
;								  Delete "$INSTDIR\QWhale.Syntax.Schemes.dll"
;									  Delete "$INSTDIR\Relkon.xml"
;										  Delete "$INSTDIR\Relkon6.exe"
;											  Delete "$INSTDIR\Relkon6.exe.config"
;												  Delete "$INSTDIR\SandDock.dll"
;													  Delete "$INSTDIR\ZedGraph.dll"
	
; 
;  RMDir /r "$INSTDIR\project"
;  RMDir /r "$INSTDIR\firmware"
;	RMDir /r "$INSTDIR\arm-gcc"
;  RMDir "$INSTDIR"


  DeleteRegKey ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY} "${PRODUCT_UNINST_KEY}" 
	DeleteRegKey HKCR ".rp6"
	DeleteRegKey HKCR "Relkon6.Project"
  SetAutoClose true
SectionEnd


